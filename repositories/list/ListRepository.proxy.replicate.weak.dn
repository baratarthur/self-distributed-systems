

component provides repositories.list.ListRepository(AdaptEvents) requires data.IntUtil iu, utils.IntArrayUtil iau, network.rpc.RPCUtil rpcUtil, data.json.JSONEncoder je, utils.PodCreatorUtil podCreator {
	Address remotes[] = new Address[]
	int addressPointer = 0
	Mutex pointerLock = new Mutex()
	
	Metadata[] buildMetaForMethod(char method[]) {
		Metadata metaMethod = new Metadata("method", method)
		return new Metadata[](metaMethod)
	}

	void ListRepository:append(store int number) {
		AppendParamsFormat params = new AppendParamsFormat(number)
		char requestBody[] = je.jsonFromData(params)
		Request req = new Request(buildMetaForMethod("append"), requestBody)
		broadcast(req)
	}

	int[] ListRepository:getList() {
		char requestBody[] = ""
		Request req = new Request(buildMetaForMethod("getList"), requestBody)
		Response res = anycast(req)
		return iau.getIntListFromString(res.content)
	}

	int ListRepository:getSum() {
		char requestBody[] = ""
		Request req = new Request(buildMetaForMethod("getSum"), requestBody)
		Response res = anycast(req)
		return iu.intFromString(res.content)
	}

	Response broadcast(Request r) {
		Response res
		mutex(pointerLock) {
			for(int i = 0; i < remotes.arrayLength; i++) {
				RequestWrapper reqWrapper = new RequestWrapper(remotes[i], r)
				res = rpcUtil.make(reqWrapper)
			}

		}

		return res
	}

	Response anycast(Request r) {
		int i = addressPointer++ % remotes.arrayLength
		RequestWrapper reqWrapper = new RequestWrapper(remotes[i], r)
		return rpcUtil.make(reqWrapper)
	}

	void AdaptEvents:active() {
		remotes = podCreator.getPodsName(3, "list-repository-replicate-weak")
		for(int i = 0; i < list.arrayLength; i++){
			append(list[i])
		}
	}

	void AdaptEvents:inactive() {
		list = getList()
		sum = getSum()
		podCreator.deleteAllPods(remotes)
	}

}

