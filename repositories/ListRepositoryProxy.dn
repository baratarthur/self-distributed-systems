uses Constants

const char debugMSG[] = "[@ListRepositoryProxy]"

component provides repositories.ListRepository requires data.IntUtil iu, rpc.RPCUtil rpc {
    Address remoteAddress[] = new Address[](new Address("localhost", 8081))

    void ListRepository:add(store Node n) {
        Request req = new Request(buildMetaForMethod(Constants.ADD), n)
        broadcastWrite(req)
    }

    Node[] ListRepository:getAll() {
        Request req = new Request(buildMetaForMethod(Constants.GET))
        Response res = broadcastRead(req, typeof(Node[]))
        Node nodes[] = res.d
        return nodes
    }

    Metadata[] buildMetaForMethod(char method[]) {
        Metadata metaMethod = new Metadata("method", method)
        return new Metadata[](metaMethod)
    }

    void broadcastWrite(Request r) {
        for(int i = 0; i < remoteAddress.arrayLength; i++) {
            rpc.connect(remoteAddress[i])
            rpc.make(r)
        }
    }

    Response broadcastRead(Request r, Type responseBodyType) {
        rpc.connect(remoteAddress[0])
        return rpc.make(r, responseBodyType)
    }

}