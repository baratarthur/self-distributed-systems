uses Constants

const char debugMSG[] = "[@ListRepositoryProxy]"

component provides repositories.ListRepository(AdaptEvents) requires data.IntUtil iu, rpc.RPCUtil rpc, data.json.JSONEncoder je {
    Address remoteAddress[] = new Address[](new Address("localhost", 8081))

    void ListRepository:add(store Node n) {
        Request req = new Request(buildMetaForMethod("add"), je.jsonFromData(n))
        broadcastWrite(req)
    }

    void ListRepository:clear() {
        Request req = new Request(buildMetaForMethod("clear"))
        broadcastWrite(req)
    }

    Node[] ListRepository:getAll() {
        Request req = new Request(buildMetaForMethod("getAll"))
        Response res = broadcastRead(req)
        Node nodes[] = je.jsonToArray(res.content, typeof(Node[]))
        return nodes
    }

    int ListRepository:size() {
        Request req = new Request(buildMetaForMethod("size"))
        Response res = broadcastRead(req)
        return iu.intFromString(res.content)
    }

    Metadata[] buildMetaForMethod(char method[]) {
        Metadata metaMethod = new Metadata("method", method)
        return new Metadata[](metaMethod)
    }

    void broadcastWrite(Request r) {
        for(int i = 0; i < remoteAddress.arrayLength; i++) {
            rpc.connect(remoteAddress[i])
            rpc.make(r)
        }
    }

    Response broadcastRead(Request r) {
        rpc.connect(remoteAddress[0])
        return rpc.make(r)
    }

    void AdaptEvents:active() {
		for(int i=0; i<list.arrayLength; i++) {
			add(list[i])
		}
	}

	void AdaptEvents:inactive() {
		list = getAll()
        index = size()
        clear()
	}

}