
component provides repositories.ListRepository(AdaptEvents) requires rpc.RPCUtil connection, data.IntUtil iu, data.json.JSONEncoder je {
	Address remotes[] = new Address[](new Address("localhost", 8081))

	void ListRepository:add (store Node node) {
		Request req0 = new Request(buildMetaForMethod("add"), je.jsonFromData(node))
		broadcastWrite(req0)
	}

	Node[] ListRepository:getAll () {
		Request req0 = new Request(buildMetaForMethod("getAll"))
		Response res = broadcastRead(req0)
		return je.jsonToArray(res.content, typeof(Node[]))
	}

	void ListRepository:clear () {
		Request req0 = new Request(buildMetaForMethod("clear"))
		broadcastWrite(req0)
	}

	int ListRepository:size () {
		Request req0 = new Request(buildMetaForMethod("size"))
		Response res = broadcastRead(req0)
		return iu.intFromString(res.content)
	}

	Metadata[] buildMetaForMethod(char method[]) {
		Metadata metaMethod = new Metadata("method", method)
		return new Metadata[](metaMethod)
	}

	void broadcastWrite(Request r) {
		for(int i = 0; i < remotes.arrayLength; i++) {
			connection.connect(remotes[i])
			connection.make(r)
		}
	}

	Response broadcastRead(Request r) {
		connection.connect(remotes[0])
		return connection.make(r)
	}

	void AdaptEvents:active() {
		for(int i=0;i<list.arrayLength;i++) {
			add(list[i])
		}
	}

	void AdaptEvents:inactive() {
		list = getAll()
		index = size()
		clear()
	}
}
