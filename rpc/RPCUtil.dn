const char debugMSG[] = "[@RPCUtil]"

component provides RPCUtil requires data.json.JSONEncoder je, net.TCPSocket, io.Output out,
    data.IntUtil iu, data.StringUtil su, Constants {
    TCPSocket connectionSocket = null

    bool RPCUtil:connect(Address a) {
        connectionSocket = new TCPSocket()
        return connectionSocket.connect(a.name, a.port)
    }

    Response RPCUtil:make(Request req, opt Type responseType) {
        Response r = null
        char requestContent[] = null

        if (req.d != null) {
            requestContent = new char[](
                je.jsonFromArray(req.meta),
                Constants.SEPARATOR,
                je.jsonFromData(req.d)
            )
            out.println(requestContent)
        } else {
            requestContent = je.jsonFromArray(req.meta)
        }
        
        requestContent = new char[](requestContent, Constants.EOF)
        connectionSocket.send(requestContent)
        char responseContent[] = receiveData(connectionSocket)
        String json[] = su.explode(responseContent, Constants.SEPARATOR)
        Metadata meta[] = je.jsonToArray(json[0].string, typeof(Metadata[]))
        if(json.arrayLength > 1) {
            Data d[] = je.jsonToArray(json[1].string, responseType)
            r = new Response(meta, d)
        } else {
            r = new Response(meta)
        }
        connectionSocket.disconnect()
        return r
    }

    char[] RPCUtil:receiveData(TCPSocket s) {
        char buf[] = null
        char body[] = null
		int stopCounter = 0
        while ((buf = s.recv(1)).arrayLength > 0) {
            body = new char[](body, buf)
			if(buf == "\r") stopCounter++
			else stopCounter = 0
			if(body.arrayLength >= 4 && stopCounter == 4) break
		}
        return su.rtrim(body)
    }

    
    Request RPCUtil:parseRequestFromString(char requestString[], opt Type dataType) {
        Request r = null
        String json[] = su.explode(requestString, Constants.SEPARATOR)
        Metadata meta[] = je.jsonToArray(json[0].string, typeof(Metadata[]))

        if(json.arrayLength > 1 && isset dataType) {
            Data d = je.jsonToData(json[1].string, dataType)
            r = new Request(meta, d)
        } else {
            r = new Request(meta)
        }

        return r
    }

    
    char[] RPCUtil:buildRawResponse(Response res) {
        if(res.d == null) return new char[](je.jsonFromArray(res.meta), Constants.EOF)
        else return new char[](je.jsonFromArray(res.meta), Constants.SEPARATOR, je.jsonFromArray(res.d), Constants.EOF)
    }

}
